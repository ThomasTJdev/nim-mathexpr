import ./mathexpr, unittest, math
const
  TestCases = [
    ("1", 1.0),
    ("1 ", 1.0),
    ("pi", 3.14159),
    ("e", 2.71828),

    ("atan(1)*4 - pi", 0.0),
    ("2+1", 2+1.0),
    ("(((2+(1))))", 2 + 1.0),

    ("3+2", 3+2.0),
    ("3+2+4", 3+2+4.0),
    ("(3+2)+4", 3+2+4.0),
    ("3+(2+4)", 3+2+4.0),
    ("(3+2+4)", 3+2+4.0),

    ("3*2*4", 3*2*4.0),
    ("(3*2)*4", 3*2*4.0),
    ("3*(2*4)", 3*2*4.0),
    ("(3*2*4)", 3*2*4.0),

    ("3-2-4", 3-2-4.0),
    ("(3-2)-4", (3-2)-4.0),
    ("3-(2-4)", 3-(2-4.0)),
    ("(3-2-4)", 3-2-4.0),

    ("3/2/4", 3.0/2.0/4.0),
    ("(3/2)/4", (3.0/2.0)/4.0),
    ("3/(2/4)", 3.0/(2.0/4.0)),
    ("(3/2/4)", 3.0/2.0/4.0),

    ("(3*2/4)", 3.0*2.0/4.0),
    ("(3/2*4)", 3.0/2.0*4.0),
    ("3*(2/4)", 3.0*(2.0/4.0)),

    ("asin sin .5", 0.5),
    ("sin asin .5", 0.5),
    ("ln exp .5", 0.5),
    ("exp ln .5", 0.5),

    ("asin sin-.5", -0.5),
    ("asin sin-0.5", -0.5),
    ("asin sin -0.5", -0.5),
    ("asin (sin -0.5)", -0.5),
    ("asin (sin (-0.5))", -0.5),
    ("asin sin (-0.5)", -0.5),
    ("(asin sin (-0.5))", -0.5),

    ("log10 1000", 3.0),
    ("log10 1e3", 3.0),
    ("log10(1000)", 3.0),
    ("log10(1e3)", 3.0),
    ("log10 1.0e3", 3.0),
    ("10^5*5e-5", 5.0),

    ("log 1000", 3.0),

    ("ln (e^10)", 10.0),
    ("100^.5+1", 11.0),
    ("100 ^.5+1", 11.0),
    ("100^+.5+1", 11.0),
    ("100^--.5+1", 11.0),
    ("100^---+-++---++-+-+-.5+1", 11.0),

    ("100^-.5+1", 1.1),
    ("100^---.5+1", 1.1),
    ("100^+---.5+1", 1.1),
    ("1e2^+---.5e0+1e0", 1.1),
    ("--(1e2^(+(-(-(-.5e0))))+1e0)", 1.1),

    ("sqrt 100 + 7", 17.0),
    ("sqrt 100 * 7", 70.0),
    ("sqrt (100 * 100)", 100.0),

    ("2^2", 4.0),
    ("pow(2,2)", 4.0),

    ("atan2(1,1)", 0.78539),
    ("atan2(1,2)", 0.463647),
    ("atan2(2,1)", 1.10711487),
    ("atan2(3,4)", 0.643501),
    ("atan2(3+3,4*2)", 0.6435),
    ("atan2(3+3,(4*2))", 0.6435),
    ("atan2((3+3),4*2)", 0.6435),
    ("atan2((3+3),(4*2))", 0.6435),
  ]
  
  Infs = [
    "1/0",
    "log(0)",
    "pow(2,10000000)",
    "fac(300)",
    "ncr(300,100)",
    "ncr(300000,100)",
    "ncr(300000,100)*8",
    "npr(3,2)*ncr(300000,100)",
    "npr(100,90)",
    "npr(30,25)",
  ]

  Pows = [
    ("2^3^4", "(2^3)^4"),
    ("-2^2", "(-2)^2"),
    ("--2^2", "2^2"),
    ("---2^2", "(-2)^2"),
    ("-2^2", "4"),
    ("2^1.1^1.2^1.3", "((2^1.1)^1.2)^1.3"),
  ]

  NaNs = [
    "0/0",
    "1%0",
    "1%(1%0)",
    "(1%0)%1",
  ]

  Combinatorics = [
    ("fac(0)", 1.0),
    ("fac(0.2)", 1.0),
    ("fac(1)", 1.0),
    ("fac(2)", 2.0),
    ("fac(3)", 6.0),
    ("fac(4.8)", 24.0),
    ("fac(10)", 3628800.0),

    ("ncr(0,0)", 1.0),
    ("ncr(10,1)", 10.0),
    ("ncr(10,0)", 1.0),
    ("ncr(10,10)", 1.0),
    ("ncr(16,7)", 11440.0),
    ("ncr(16,9)", 11440.0),
    ("ncr(100,95)", 75287520.0),

    ("npr(0,0)", 1.0),
    ("npr(10,1)", 10.0),
    ("npr(10,0)", 1.0),
    ("npr(10,10)", 3628800.0),
    ("npr(20,5)", 1860480.0),
    ("npr(100,4)", 94109400.0),
  ]

proc `~=`(a, b: float): bool = 
  ## Checks if difference between two floats is less than 0.0001
  abs(a - b) < 1e-4

suite "Eval test cases":
  for data in TestCases:
    let (expr, expected) = data
    test(expr & " == " & $expected):
      check eval(expr) ~= expected
  
  for expr in Infs:
    test(expr & " == Infinity"):
      check abs(eval(expr)) == Inf
  
  for data in Pows:
    let (first, second) = data
    test(first & " == " & second):
      let (a, b) = (eval(first), eval(second))
      check a == b
  
  for expr in NaNs:
    test(expr & " == " & "NaN"):
      check(eval(expr).classify == fcNan)
  
  for data in Combinatorics:
    let (expr, expected) = data
    test(expr & " == " & $expected):
      check eval(expr) ~= expected

suite "Custom functions/variables":
  test "Custom function":
    proc myData(args: seq[float]): float = 
      for arg in args: result += arg * 2
  
    functions["test"] = myData

    check eval(
      "test(1, 2, 3, 4, 5) + 35 - 27"
    ) == (myData(@[1.0, 2, 3, 4, 5]) + 35 - 27)
  
  test "Custom variables":
    check eval(
      "x + y - fac(z)", 
      {"x": 5.0, "y": 36511.0, "z": 5.0}
    ) == (5.0 + 36511 - 120)